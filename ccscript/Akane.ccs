KissuStand:
	define MetAkane = flag 977	//see datacrystal for a list of unused flags
	define MetAkaneAsFriend = flag 983

	"[19 10 01]"    //checks first party member ID
    if not result_is(1)
    {
        goto(SomeoneElse)
    }
	else
	{
		goto(Akari)
	}

Akari:
	if not MetAkane
	{
		"@What?" pause(30) next
		"@It's a living!"
		set(MetAkane)
		end
	}
	else 
	{
		"@So|.|.|." next
		"@Are you interested?" next
		"@It's $20 a pop." newline
		menu
		{
			"Yes":
			{
				if hasmoney(20) 
				{
					"@Oh,/ I could never charge you for that,/ {name(1)}." next
					"@C'mere!" next
					goto (Kissu)
				}
				else 
				{
					"@Oh,/ you don't have enough?" next
					"@Don't worry,/ this one's on the house (wink)." next
					goto (Kissu)
				}
			}
			"No": 
			{
				"@Y-yeah,/ who'd kiss their sister,/ r-right?" next
				"@Haha." end
			}
		}
	}

Kissu:
	"[19 10 01]{swap}@(Akane gives [1C 02 00] a big kiss.)" next
	pause(700) "@(It goes on for longer than you'd expect.)" next
	pause(300) "@What do you mean that's not worth $20?!" end

SomeoneElse:
	if not MetAkaneAsFriend
	{
		"@Akari's friend?" next
		"@Don't judge me."
		set(MetAkaneAsFriend)
		end
	}
	else
	{
		"@So|.|.|." next
		"@Are you interested?" next
		"@It's $20 a pop." newline
		menu
		{
			"Yes":
			{
				if hasmoney(20) 
				{
					takemoney(20)
					sound(SND_KACHING)
					"@Oh,/ um|.|.|." next
					"@C'mere...| I guess?" next
					goto (Kissu)
				}
				else 
				{
					"@Oh,/ you don't have enough?" next
					"@(Dodged a bullet there!)" end
				}
			}
			"No": 
			{
				"@Don't tell anyone how I live." end
			}
		}

	}

Bat:
	define SeenSign = flag 978
	define GotBat = flag 805

	"[19 10 01]"    //checks first party member ID
    if not result_is(1)
    {
        goto(NotAkariBat)
    }

	if not GotBat
	{
		"@{name(1)},/ it's pretty dangerous out there." next
		"@You might want to take a bat with you to be safe." next
		"@I think I have one lying around somewhere|.|.|.|" next
		if SeenSign
		{
			"@My room is locked?" next
			"@{name(1)},/ what have I told you about trying to go in in my room?" pause(60) next
		}
		"@Ah,/ here you go!" next
		"@(Akane produces a cracked bat.)" next
		Functions.GetItem(17,NoRoom)
		set(GotBat) newline
		"@Make sure you equip it,/ otherwise it's pretty useless." end
	}
	else
	{
		if SeenSign
		{
			"@{name(1)},/ what have I told you about trying to go in in my room?" pause(60) end
		}
		else
		{
			"@Please stay safe, {name(1)}." end
		}
	}

NotAkariBat:
	if not GotBat
	{
		"@You're {name(1)}'s friend,/ right?" next
		"@Give this to her,/ it might come in handy." next
		Functions.GetItem(17,NoRoom)
		set(GotBat)
		end
	}
	else
	{
		"@I'm not a charity,/ I've only got one of those." end
	}

NoRoom:
	"@Looks like you're carrying too much." next
	"@Talk to me again when you've cleared some things out."
	unset(805)
	end

Heal:
	"[19 10 01]"    //checks first party member ID
    if result_is(1)
    {
		"@Hey, {name(1)}." next
		goto(Heal2)
	}
	else
	{
		"@Say,/ you're {name(1)}'s friend,/ right?" next
		goto(Heal2)
	}

Heal2:
	"@I got some [1C 01 04] earlier,/ please have some some.[03]" healall "{music(12)}{pause(200)}{music_resume}" eob