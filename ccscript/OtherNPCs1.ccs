command e(label) "{long label}" //use this coilsnake gets confused over e

TheDog:
    "@(Yeah, it's a lazy dog dangling afternoon.)" next
    "@(You should call Ayano and give her an update if anything happens,/" next
    "@I'm sure she'd appreciate it.)" end

GirlOutsideLibrary:
    "@A library is a good place to learn things." next
    "@Shame all the good stuff is reference only." next
    "@They let you take out maps for free, though." next
    "@Isn't that nice of them?" end

HillGuy:
    "@I've seen many clouds in my time." next
    "@But I must say,/ these are by far the strangest looking ones I've seen." end

KissGuy:
    "@When you're in trouble, do you think you'd ever like to have a kiss?" newline
    menu 
    {
        "Yes" : 
        {
            "@The girl in the next house is offering a service like that." end

        }
        "No" : 
        {
            "@Yeah,/ I guess that is kinda weird." end
        }
    }

OtherHillGuy:
    "@Call it a hunch, but I get the feeling you'll be wasting your time going up this hill." end

KyoukoDemo:
    define GotKyouko = flag 982
    "@What's that,/ {name(1)}?" next
    "@You want me in the demo?" next
    "@Well, if it's for you." next          //For the sake of documentation, 1F 1E is the code for hiding a sprite, B6 00 is Kyouko, 00 06 is the effect
    window_closeall "{music(11)}{pause(120)}{pause(120)}{pause(120)}{pause(60)}{music_resume}{window_open(1)}@[1C 02 02] joined you.[03] [1F 1E B6 00 06][1F 11 02] {window_closeall}" set(GotKyouko) eob

DemoPolice1:
    "@Slow down there,/ missy." next
    "@Go any further in this demo and I'm going to have to give you a ticket!" end

DemoPolice2:
    "@That face you just made." next
    "@It's that look that make this job worth doing." end

MapLady:
    "@Hello there!" next
    "[06 97 02 {e(ReturnMap)}][06 FF 01 {e(ReturnMap)}][06 59 00 {e(OvercomingShyness)}]@Pretty much everything here is reference only." next
    "@Apart from the maps,/ we have tonnes of those." next
    "@Do you want to borrow one?" linebreak
    "[19 02]Yes[02][19 02]No[02][1C 07 02][11]{clearline}[09 02 {e(data_27.l_0xc732e1)} {e(DontTakeMap)}]" goto(DontTakeMap)

DontTakeMap:
    "@A cute girl like you without a map won't be popular with the girls." next
    "@Do you want one now?" linebreak
    "[19 02]Yes[02][19 02]No[02][1C 07 02][11]{clearline}[09 02 {e(data_27.l_0xc732e1)} {e(DontTakeMap)}]" goto(DontTakeMap)

ReturnMap:
    "@You don't need to give us that map back just yet." next
    "@Come back in the year 2021 to return it." end

OvercomingShyness:
    "@Please return <Overcoming Shyness> later!" 
    
ShyGuy:                         //originally for hte shy guy in the library, but he's actually important later, so put this on someone else maybe
    define ShyGuy = flag 984
    if not ShyGuy
    {
        counter_zero
        set(ShyGuy)
    }
    inc
    if counter_is(10)
    {
        unset(ShyGuy)
        "@What can I say?" next
        "@I'm a shy guy." end
    }
    else
    {
    "@....." linebreak
    "       ....." linebreak
    "            ....." end
    }